// Assumes polyglot.jar is in classpath
include "polyglot/parse/java12.cup"

package fabil.parse;

import fabil.ast.Atomic;
import fabil.ast.FabILNodeFactory;
import fabil.ast.FabricArrayInit;
import fabil.ast.FabricArrayTypeNode;
import polyglot.parse.*;
import fabil.types.FabILTypeSystem;
import fabil.types.FabILFlags;
import fabil.parse.Name;
import java.net.URI;

parser Grm extends polyglot.parse.Grm {:
    public final FabILTypeSystem ts;
    public final FabILNodeFactory nf;
	public final URI ns;
	
    public Grm(Lexer l, TypeSystem t, NodeFactory n, ErrorQueue q, URI ns) {
        super(l, t, n, q);
        ts = (FabILTypeSystem) t;
        nf = (FabILNodeFactory) n;
		this.ns = ns; 
    }
    
    public TypeNode fabricArray(TypeNode n, int dims) throws Exception {
      if (dims <= 0) return n;
      
      if (n instanceof ArrayTypeNode && !(n instanceof FabricArrayTypeNode))
        return javaArray(n, dims);
      
      if (n instanceof CanonicalTypeNode) {
        Type t = ((CanonicalTypeNode) n).type();
        return nf.CanonicalTypeNode(pos(n), ts.fabricArrayOf(t, dims));
      }
        
      return nf.FabricArrayTypeNode(pos(n), fabricArray(n, dims - 1));
    }
    
    public TypeNode array(TypeNode n, int dims) throws Exception {
      return fabricArray(n, dims);
    }
    
    public TypeNode javaArray(TypeNode n, int dims) throws Exception {
      if (dims > 0 && n instanceof AmbTypeNode) {
        return nf.ArrayTypeNode(pos(n), javaArray(n, dims - 1));
      }
      
      return super.array(n, dims);
    } 
    

:};

terminal Token AT;
terminal Token ATOMIC;
terminal Token ABORT;
terminal Token RETRY;
terminal Token STAGE;
terminal Token NONFABRIC;

non terminal Atomic atomic_statement;
non terminal Expr location_expr_opt;
non terminal Expr label_opt;
non terminal Stmt abort_stmt, retry_stmt, stage_stmt;
non terminal FabricArrayInit fabric_array_initializer;

start with goal;
//////////////
// Names 	//
//////////////
// The parser sometimes creates unambiguous package objects, 
//	providing the namespace preserves this ability
override simple_name ::=
                    // Name
        IDENTIFIER:a
            {: RESULT = new Name(parser.nf, parser.ts, parser.ns, parser.pos(a), parser.nf.Id(parser.pos(a), a.getIdentifier())); :}
    ;
    
override qualified_name ::=
                    // Name
        name:a DOT IDENTIFIER:b
            {: RESULT = new Name(parser.nf, parser.ts, parser.ns, parser.pos(a, b), a, parser.nf.Id(parser.pos(b), b.getIdentifier())); :}
    ;
    
override identifier_opt ::=
                    // Name
            {: RESULT = null; :}
    |   IDENTIFIER:a
            {: RESULT = new Name(parser.nf, parser.ts, parser.ns, parser.pos(a),
                parser.nf.Id(parser.pos(a), a.getIdentifier())); :}
    ;

////////////////////////////////////////////////////////////////////////
// Atomic Sections                                                    //
////////////////////////////////////////////////////////////////////////

extend statement_without_trailing_substatement ::=
                    // Stmt
        atomic_statement:a {: RESULT = a; :}
    |   abort_stmt:a {: RESULT = a; :}
    |   retry_stmt:a {: RESULT = a; :}
    |   stage_stmt:a {: RESULT = a; :}
    ;

atomic_statement ::=
                    // Atomic
        ATOMIC:n block:a {: RESULT = parser.nf.Atomic(parser.pos(n,a), a.statements()); :};

abort_stmt ::=
					// AbortStmt
		ABORT:a SEMICOLON:b
			{: RESULT = parser.nf.AbortStmt(parser.pos(a, b)); :}
	;
	
retry_stmt ::= 
					// RetryStmt
		RETRY:a SEMICOLON:b
			{: RESULT = parser.nf.RetryStmt(parser.pos(a, b)); :}
	;

stage_stmt ::=
					// RetryStmt
		STAGE:a SEMICOLON:b
			{: RESULT = parser.nf.StageStmt(parser.pos(a, b)); :}
	;

extend modifier ::=
        ATOMIC:a
            {: RESULT = FabILFlags.ATOMIC; :}
    |   NONFABRIC:a
    		{: RESULT = FabILFlags.NONFABRIC; :}
    ;

////////////////////////////////////////////////////////////////////////
// Array types                                                        //
////////////////////////////////////////////////////////////////////////

extend array_type ::=
                    // TypeNode
        primitive_type:a NATIVE dims:b
            {: RESULT = parser.javaArray(a, b.intValue()); :}
    |   name:a NATIVE dims:b
            {: RESULT = parser.javaArray(a.toType(), b.intValue()); :}
    ;
    
extend cast_expression ::=
                    // Cast
        LPAREN:p primitive_type:a NATIVE dims:b RPAREN unary_expression:c
            {: RESULT = parser.nf.Cast(parser.pos(p, c,a),
                parser.javaArray(a, b.intValue()), c); :}
    |   LPAREN:p name:a NATIVE dims:b RPAREN unary_expression_not_plus_minus:c
            {: RESULT = parser.nf.Cast(parser.pos(p, c,a),
                parser.javaArray(a.toType(), b.intValue()), c); :}
    ;
    
////////////////////////////////////////////////////////////////////////
// Labels and locations for constructors                              //
////////////////////////////////////////////////////////////////////////

label_opt ::=
		    // Expr
            {: RESULT = null; :}
    |   COMP name:l
    	    {: RESULT = l.toExpr(); :}
    |   COMP THIS:a
    	    {: RESULT = parser.nf.This(parser.pos(a)); :}
    ;

location_expr_opt ::=
                    // Expr
            {: RESULT = null; :}
    |   AT name:a
            {: RESULT = a.toExpr(); :}
    ;

override class_instance_creation_expression ::=
                    // Expr
        NEW:n class_type:a location_expr_opt:loc LPAREN argument_list_opt:b RPAREN:d
            {: RESULT = parser.nf.New(parser.pos(n, d), a, loc, b); :}
    |   NEW:n class_type:a location_expr_opt:loc LPAREN argument_list_opt:b RPAREN class_body:c
            {: RESULT = parser.nf.New(parser.pos(n, c), a, loc, b, c); :}
    |   primary_no_new_array:a DOT NEW simple_name:b location_expr_opt:loc LPAREN argument_list_opt:c RPAREN:d
            {: RESULT = parser.nf.New(parser.pos(a, d), a, b.toType(), loc, c); :}
    |   primary_no_new_array:a DOT NEW simple_name:b location_expr_opt:loc LPAREN argument_list_opt:c RPAREN class_body:d
            {: RESULT = parser.nf.New(parser.pos(a, d), a, b.toType(), loc, c, d); :}
    |   name:a DOT NEW simple_name:b location_expr_opt:loc LPAREN argument_list_opt:c RPAREN:d
            {: RESULT = parser.nf.New(parser.pos(a, d), a.toExpr(), b.toType(), loc, c); :}
    |   name:a DOT NEW simple_name:b location_expr_opt:loc LPAREN argument_list_opt:c RPAREN class_body:d
            {: RESULT = parser.nf.New(parser.pos(a, d), a.toExpr(), b.toType(), loc, c, d); :}
    ;

override array_creation_expression ::=
                    // NewArray
        NEW:n primitive_type:a NATIVE dim_exprs:b dims_opt:c
            {: RESULT = parser.nf.NewArray(parser.pos(n, b), a, b,
                c.intValue()); :}
    |   NEW:n primitive_type:a dim_exprs:b dims_opt:c label_opt:l location_expr_opt:loc
            {: RESULT = parser.nf.NewFabricArray(parser.pos(n, b), a, l, null, loc, b,
                c.intValue()); :}
    |   NEW:n class_or_interface_type:a NATIVE dim_exprs:b dims_opt:c
            {: RESULT = parser.nf.NewArray(parser.pos(n, b), a, b,
                c.intValue()); :}
    |   NEW:n class_or_interface_type:a dim_exprs:b dims_opt:c label_opt:l location_expr_opt:loc
            {: RESULT = parser.nf.NewFabricArray(parser.pos(n, b), a, l, null, loc, b,
                c.intValue()); :}
    |   NEW:n primitive_type:a NATIVE dims:b array_initializer:c
            {: RESULT = parser.nf.NewArray(parser.pos(n, c), a,
                b.intValue(), c); :}
    |   NEW:n primitive_type:a dims:b label_opt:l location_expr_opt:loc fabric_array_initializer:c
            {: RESULT = parser.nf.NewFabricArray(parser.pos(n, c), a, l, null, loc,
                b.intValue(), c); :}
    |   NEW:n class_or_interface_type:a NATIVE dims:b array_initializer:c
            {: RESULT = parser.nf.NewArray(parser.pos(n, c), a,
                b.intValue(), c); :}
    |   NEW:n class_or_interface_type:a dims:b label_opt:l location_expr_opt:loc fabric_array_initializer:c
            {: RESULT = parser.nf.NewFabricArray(parser.pos(n, c), a, l, null, loc,
                b.intValue(), c); :}
    ;

fabric_array_initializer ::=
		    // FabricArrayInit
	array_initializer:a
	    {: RESULT = parser.nf.FabricArrayInit(a.position(), a.elements()); :}
    ;

//
// In order to prevent ambiguity, we disallow dereferencing (.) a new array:
//   new Foo[]@a.b means new Foo[]@(a.b) rather than (new Foo[]@a).b
// Note that this also disallows new Foo[].length which we could allow
//
// The following updated productions are to enforce this.
//

// class_instance_creation_expression modified above

// field_access
drop {
    field_access ::=
            primary:a DOT IDENTIFIER:b
        ;
}
extend field_access ::=
        primary_no_new_array:a DOT IDENTIFIER:b
            {: RESULT = parser.nf.Field(parser.pos(a, b, b), a,
                parser.nf.Id(parser.pos(b), b.getIdentifier())); :}
    ;

// method_invocation
drop {
    method_invocation ::=
	        name LPAREN argument_list_opt RPAREN
        |   primary DOT IDENTIFIER LPAREN argument_list_opt RPAREN
        ;
}

extend method_invocation ::=
                    // Call
        name:a location_expr_opt:b LPAREN argument_list_opt:c RPAREN:d
            {: RESULT = parser.nf.Call(parser.pos(a,d),
                a.prefix == null ? null : a.prefix.toReceiver(),
                a.name, b, c); :}
    |   primary_no_new_array:a DOT IDENTIFIER:b location_expr_opt:c LPAREN argument_list_opt:d RPAREN:e
            {: RESULT = parser.nf.Call(parser.pos(b,e), a,
                parser.nf.Id(parser.pos(b), b.getIdentifier()), c, d); :}
    ;

// explicit_constructor_invocation
// TODO: this doesn't seem right.
drop {
    explicit_constructor_invocation ::=
            primary DOT THIS LPAREN argument_list_opt RPAREN SEMICOLON
        |   primary DOT SUPER LPAREN argument_list_opt RPAREN SEMICOLON
        ;
}
extend explicit_constructor_invocation ::=
        primary_no_new_array:a DOT THIS:n LPAREN argument_list_opt:b RPAREN SEMICOLON:c
            {: RESULT = parser.nf.ThisCall(parser.pos(a, c, n), a, b); :}
    |   primary_no_new_array:a DOT SUPER:n LPAREN argument_list_opt:b RPAREN SEMICOLON:c
            {: RESULT = parser.nf.SuperCall(parser.pos(a, c, n), a, b); :}
    ;

// Turn final fields into immutable fields.
drop {
    field_declaration ::=
            modifiers_opt type variable_declarators SEMICOLON
        ;
}
field_declaration ::=
                    // List of ClassMember
        modifiers_opt:a type:b variable_declarators:c SEMICOLON:e
            {: List<ClassMember> l = new LinkedList<>();
               // Turn 'final' flags into 'immutable'.
               if (a.isFinal()) {
                   a = a.clearFinal().set(FabILFlags.IMMUTABLE);
               }
               
               for (VarDeclarator d : c) {
                   l.add(parser.nf.FieldDecl(parser.pos(b, e),
                                             a, parser.array(b, d.dims),
                                             d.name, d.init, parser.javadoc(parser.pos(a), parser.pos(b))));
               }
               RESULT = l;
            :}
    ;
